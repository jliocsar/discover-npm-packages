{ argv, exit } from node:process
{ fetch } from undici
pino from pino
pretty from pino-pretty
minimist from minimist
removeMd from remove-markdown
{ Octokit } from octokit
type { TPackageParent, TPackage, TPackageDownloads } from ./types.civet
{ url, curry, ascByDownloads, writeYaml } from ./utils.civet

NPM_REGISTRY_URL := 'https://registry.npmjs.org'
NPM_DOWNLOADS_API_URL := 'https://api.npmjs.org/downloads/point/last-month'
NPM_PACKAGE_URL := 'https://www.npmjs.com/package'
HARD_CRAWL_LIMIT := 5
DEFAULT_CRAWL_LIMIT := 2

octokit := new Octokit auth: process.env.GITHUB_PERSONAL_ACCESS_TOKEN!
logger := pino pretty { +colorize, +singleLine }

{ _: packages, crawl: crawlCount } := minimist argv.slice(2),
  alias: { c: 'crawl' }
  default: { crawl: DEFAULT_CRAWL_LIMIT }
adjustedCrawlLimit .= crawlCount

if adjustedCrawlLimit > HARD_CRAWL_LIMIT
  logger.warn 'Crawl limit is too high! Max. is %d', HARD_CRAWL_LIMIT
  exit 1

logger.info 'Running for %d packages, using crawl limit of %d', packages.length, adjustedCrawlLimit

operator fromBufferOf := Buffer.from

getNpmPackage := (pkg: string) ->
  fetch url NPM_REGISTRY_URL, `${pkg}/latest` |> await |> .json()

getNpmPackagesDownloads := (pkg: string) ->
  fetch url NPM_DOWNLOADS_API_URL, pkg |> await |> .json()

getReadme := (owner: string, repo: string) ->
  octokit.rest.repos.getReadme { owner, repo }
    |> await
    |> .data.content
    |> & fromBufferOf 'base64'
    |> .toString()

getReadmePreview := (repositoryUrl: string) ->
  [owner, repo] := repositoryUrl
    |> .replace /^git:\/\/github\.com\//, ''
    |> .replace /\.git$/, ''
    |> .split '/'
  getReadme owner, repo
    |> await
    |> removeMd
    |> .replace /\n+/g, '\n'
    |> .slice 0, 1000
    |> & + '...'

isGithub := (npmPkg: TPackage) ->
  npmPkg.repository? and /^git:\/\/github\.com/.test npmPkg.repository.url

crawlNpmPackages := curry (crawls = 0, pkgs: string[]) ->
  await.all
    for pkg of pkgs
      npmPkg := (await getNpmPackage pkg) as TPackage
      npmPkgLastMonthDownloads := (await getNpmPackagesDownloads pkg) as TPackageDownloads
      parent : TPackageParent := {
        name: pkg
        url NPM_PACKAGE_URL, pkg
        npmPkg{description}
        npmPkgLastMonthDownloads{downloads}
        readmePreview: if isGithub npmPkg then await getReadmePreview npmPkg.repository.url
        children: unless crawls is adjustedCrawlLimit
          Object.assign {}, npmPkg.dependencies, npmPkg.devDependencies, npmPkg.peerDependencies
            |> Object.keys
            |> crawlNpmPackages crawls + 1
            |> await
            |> .sort ascByDownloads
      }
      logger.info parent
      parent

main := ->
  packages
    |> crawlNpmPackages()
    |> await
    |> writeYaml 'output.yaml'

await main()
